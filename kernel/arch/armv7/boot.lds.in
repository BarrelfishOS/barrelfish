/*
 * Copyright (c) 2007,2008,2016 ETH Zurich.
 * All rights reserved.
 *
 * This file is distributed under the terms in the attached LICENSE file.
 * If you do not find this file, copies can be found by writing to:
 * ETH Zurich D-INFK, Haldeneggsteig 4, CH-8092 Zurich. Attn: Systems Group.
 */

#include <offsets.h>

OUTPUT_FORMAT("elf32-littlearm")
OUTPUT_ARCH("arm")

ENTRY(start)

SECTIONS {
    . = 0;

    . = ALIGN(4k);
    boot_start = .;

    .text : {
        *(.text);
    }

    .text.unlikely : { *(.text.unlikely); }

    boot_text_end = .;

    . = ALIGN(4k);
    .rodata . :
    {
        *(.rodata);
    }

    .got . :
    {
        got_base = .;
        *(.got);
    }

    /* The page tables need a larger alignment. */
    . = ALIGN(16k);
    .boot . :
    {
        *(.boot.tables);
        *(.boot);
    }

    .bss . :
    {
        *(.bss);
    }

    boot_end = .;

    /***** These sections get discarded *****/
    /DISCARD/ :
    {
        /* Discard exception handler frames and headers -- we don't use em */
        *(.eh_frame);
        *(.eh_frame_hdr);
        *(.note.gnu.build-id);
        *(.interp);
/*           *(.dynsym); */
/*           *(.dynstr); */
/*           *(.hash); */
/*           *(.gnu.hash); */
        *(.dynamic); 
    }
}
